# Model to fit population growth (with density dependence/stochasticity) to 
# time series of survey counts: includes habitat-specific K
#
model{
# OBSERVED VARIABLES
# Observed node: survey counts 
# NOTE: negative binomial, with dispersion set so that V = 1.2*M
# (ie if M = N[Ysrv[i],Psrv[i]], then r = M^2/(V-M), p = r/(r+M), Count ~ dnegbin(p,r)
for (i in 1:Ncounts){		
	MI[i] <- N[Ysrv[i],Psrv[i]]
	VI[i] <- N[Ysrv[i],Psrv[i]]*Vinflate
	rI[i] <- MI[i]^2/(VI[i]-MI[i]) 
	pI[i] <- rI[i]/(rI[i]+MI[i]) 
	CountI[i] ~ dnegbin(pI[i],rI[i])	
	MP[i] <- max(.001,n[1,Ysrv[i],Psrv[i]])*pupscalefact
	VP[i] <- MP[i]*Vinflate
	rP[i] <- MP[i]^2/(VP[i]-MP[i]) 
	pP[i] <- rP[i]/(rP[i]+MP[i]) 
	CountP[i] ~ dnegbin(pP[i],rP[i])
}
# Observed Node: male and female carcasses (COD = 1 for shark, 0 for other)
for (i in 1:NCarcsF){
	OthHazF[i] <- exp(.8*zeta[2]+zeta[4]*PrpK[PshkF[i],YshkF[i]]+UB[PshkF[i],YshkF[i]]*UBE)
	ShkHazF[i] <- exp(.2*zeta[2]+AdHz[PshkF[i],YshkF[i]])
	ExpPshkF[i] <- ShkHazF[i]/(ShkHazF[i]+OthHazF[i])
	CarcShrkF[i] ~ dbin(ExpPshkF[i],TotCarcF[i])
}
for (i in 1:NCarcsM){
	OthHazM[i] <- exp(.8*zeta[2]+.7*zeta[3]+zeta[4]*PrpK[PshkM[i],YshkM[i]]+UB[PshkM[i],YshkM[i]]*UBE)
	ShkHazM[i] <- exp(.2*zeta[2]+.3*zeta[3]+Mbias*AdHz[PshkM[i],YshkM[i]])
	ExpPshkM[i] <- ShkHazM[i]/(ShkHazM[i]+OthHazM[i])
	CarcShrkM[i] ~ dbin(ExpPshkM[i],TotCarcM[i])
}
# LATENT VARIABLES
# Loop through Pops to initiate populations
for (i in 1:Npop){
	# Initialize pop vectors for SubPop i in pre-year 1 (vague prior centered around 90% initial counts)
	logN1[i] ~ dnorm(.9*logN0[i],0.1)
	# Dns0[i] ~ dunif(0,5) # Uninformative uniform prior
	Np[1,i] <- max(1,round(exp(logN1[i])-.5))	
	# Proportion of females depends on initial demographic composition
	# (Male areas, transition areas or female-dominated areas)
	NF0[i] ~ dbin(propfem[DemComp[i,1]],Np[1,i])
	np[1,1,i] <- NF0[i] 
	np[2,1,i] <- Np[1,i]-np[1,1,i]
	PrpKp[i,1] <- Np[1,i]/K[i]
	# N[1,i] <- Np[NyrsPre,i]
	# n[1,1,i] <- np[1,NyrsPre,i]
	# n[2,1,i] <- np[2,NyrsPre,i]
	# PrpK[i,1] <- PrpKp[i,NyrsPre]	
	N[1,i] <- Np[1,i]
	n[1,1,i] <- np[1,1,i]
	n[2,1,i] <- np[2,1,i]
	PrpK[i,1] <- PrpKp[i,1]
}
# Loop through a number of "pre" years to allow pop structure to stabilize
# for (y in 1:(NyrsPre-1)){
	# for (i in 1:Npop){
		# Rp[i,y] <- min(rho[1],((rho[2]^(-rho[3]*PrpKp[i,y]))/10 + rho[4]))
        # fsp[i,y] <- exp(-zeta[1]*exp(zeta[2]+zeta[4]*PrpKp[i,y]+AdHz[i,1]))
        # msp[i,y] <- exp(-zeta[1]*exp(zeta[2]+zeta[3]+zeta[4]*PrpKp[i,y]+Mbias*AdHz[i,1]))
		# # Build projection matrix for year y, subpop i
		# Mp[1,1,y,i] <- fsp[i,y]*(1+Rp[i,y])
		# Mp[1,2,y,i] <- 0
		# Mp[2,1,y,i] <- fsp[i,y]*Rp[i,y]         
		# Mp[2,2,y,i] <- msp[i,y]
		# # Demographic transitions, via Matrix multiplication
		# n1p[1:2,y,i] <- Mp[1:2,1:2,y,i]%*%np[1:2,y,i]
		# nf1p[i,y] <- n1p[1,y,i]
		# nm1p[i,y] <- n1p[2,y,i]
		# # Create dispersal matrices, account for reduced female dispersal to male areas
		# # and increased male dispersal to those areas
		# DAfp[i,1:Npop,y] <- FRDF[DemComp[i,1]]*disprobF[i,1:Npop] 
		# #DRf[1:Npop,i,y] ~ ddirch(50*DAf[1:Npop,i,y])
		# DRfp[1:Npop,i,y] <- DAfp[1:Npop,i,y]/sum(DAfp[1:Npop,i,y])
		# DAmp[i,1:Npop,y] <- ((1/FRDF[DemComp[i,1]])^.1)*disprobM[i,1:Npop] 
		# #DRm[1:Npop,i,y] ~ ddirch(25*DAm[1:Npop,i,y])
		# DRmp[1:Npop,i,y]	<- DAmp[1:Npop,i,y]/sum(DAmp[1:Npop,i,y])
	# }
	# # Redistribute males and females: multiply by dispersal matrices
	# nfp[1:Npop,y] <- DRfp[1:Npop,1:Npop,y]%*%nf1p[1:Npop,y]
	# nmp[1:Npop,y] <- DRmp[1:Npop,1:Npop,y]%*%nm1p[1:Npop,y]	
	# for (i in 1:Npop){
		# np[1,y+1,i] <- nfp[i,y]
		# np[2,y+1,i] <- nmp[i,y]
		# Np[y+1,i] <- np[1,y+1,i]+np[2,y+1,i]
		# # Calculate proportion of K for SubPop i in year y,
		# PrpKp[i,y+1] <- Np[y+1,i]/K[i]
	# }	
# }
# Loop through Pops and Years to compute population dynamics from Year 1 = 1983
for (y in 1:(Nyrs-1)){
	for (i in 1:Npop){
		# Calculate effects of Environmental stochasticity
		eps[i,y] ~ dnorm(0,tauE) T(-.8,.8)  # env. stochasticity, variance in log hazard ratios
		# NOTE: EPS CONSTRAINED TO RESULT IN BIOLOGICALLY REALISTIC VALUES
		epsR[i,y] <- exp(-1*eps[i,y])  # log-normal error for R
		# Calculate vital rates, with effects of D-D, additional shark hazards, stochasticity
		#   and "urchin boom (UB) effect" in some areas/years
		R[i,y] = min(rho[1],((rho[2]^(-rho[3]*PrpK[i,y]))/10 + rho[4])*epsR[i,y])
        fs[i,y] = exp(-zeta[1]*exp(zeta[2]+zeta[4]*PrpK[i,y]+UB[i,y]*UBE+AdHz[i,y]+eps[i,y]))
        ms[i,y] = exp(-zeta[1]*exp(zeta[2]+zeta[3]+zeta[4]*PrpK[i,y]+UB[i,y]*UBE+Mbias*AdHz[i,y]+eps[i,y]))
		# Build projection matrix for year y, subpop i
		M[1,1,y,i] <- fs[i,y]*(1+R[i,y])
		M[1,2,y,i] <- 0
		M[2,1,y,i] <- fs[i,y]*R[i,y]         
		M[2,2,y,i] <- ms[i,y]
		# Demographic transitions, via Matrix multiplication
		n1[1:2,y,i] <- M[1:2,1:2,y,i]%*%n[1:2,y,i]
		nf1[i,y] <- n1[1,y,i]
		nm1[i,y] <- n1[2,y,i]
		# Create dispersal matrices, account for reduced female dispersal to male areas
		# and increased male dispersal to those areas
		DAf[i,1:Npop,y] <- FRDF[DemComp[i,y]]*disprobF[i,1:Npop] 
		#DRf[1:Npop,i,y] ~ ddirch(50*DAf[1:Npop,i,y])
		DRf[1:Npop,i,y] <- DAf[1:Npop,i,y]/sum(DAf[1:Npop,i,y])
		DAm[i,1:Npop,y] <- ((1/FRDF[DemComp[i,y]])^.1)*disprobM[i,1:Npop] 
		#DRm[1:Npop,i,y] ~ ddirch(25*DAm[1:Npop,i,y])
		DRm[1:Npop,i,y]	<- DAm[1:Npop,i,y]/sum(DAm[1:Npop,i,y])
	}
	# Redistribute males and females: multiply by dispersal matrices
	nf[1:Npop,y] <- DRf[1:Npop,1:Npop,y]%*%nf1[1:Npop,y]
	nm[1:Npop,y] <- DRm[1:Npop,1:Npop,y]%*%nm1[1:Npop,y]	
	for (i in 1:Npop){
		n[1,y+1,i] <- nf[i,y]
		n[2,y+1,i] <- nm[i,y]
		N[y+1,i] <- n[1,y+1,i]+n[2,y+1,i]
		# Calculate proportion of K for SubPop i in year y,
		PrpK[i,y+1] <- N[y+1,i]/K[i]
	}	
}
# Loop through Pops to estimate K (sum over grid cells, grouped into depth bin reps)
for (i in 1:Npop) {
  # Allow for variance in realized K density across coastal sections 
  # (assume log-normal distribution in K density)
  epsK[i] ~ dnorm(0,tauK) # T(-1.96,1.96)
  LogKdns[1,i] <- IntcptE+epsK[i] 
  LogKdns[2:Nbin[i],i] <- Intcpt+DPin[Dep[2:Nbin[i],i]]+DPoff[Dep[2:Nbin[i],i]]+B3*Phrd[2:Nbin[i],i]+B4*Pklp[2:Nbin[i],i]+B5*Dsh[2:Nbin[i],i]+epsK[i]
  for (j in 1:Nbin[i]){
	KdnsR[j,i] <- exp(LogKdns[j,i])
	KB[j,i] <- KdnsR[j,i]*AreaB[j,i]
	ExpctCountB[j,i] <- sum(PrpK[i,1:Nyrs])*KB[j,i]
	# Observed node: summed count for this depth bin/sub-pop
	ppar[j,i] <- Nu/(Nu+ExpctCountB[j,i]) 
	Sumcount[j,i] ~ dnegbin(ppar[j,i],Nu)	
  }	
  K[i] <- sum(KB[1:Nbin[i],i]) 
  Kdns[i] <- K[i]/Areas[i]
  # Observed node, "K guess" with large variance: log of 2005-2010 counts
  for (j in 1:NKguess){
	Kguess[j,i] ~ dnorm(log(K[i]),tauKg)
  }  
}
# Compute additional Shark bite hazards by sub-pop and year
#  (Conditional Autoregressive variable in 2 dimensions, coast section and time)
for (y in 1:(Nyrs)){
	for (i in 1:Npop){
		AdHz[i,y] <- ifelse(i==5,0,5*ilogit(AdHzP[i,y]))
	}
}
AdHzP[1,1] ~ dt(-7, 1/2^2 ,1) 
AdHzP[2,1] ~ dt(-7, 1/2^2 ,1) 
AdHzP[1,2] ~ dt(-7, 1/2^2 ,1) 
for (i in 3:Npop) {
    diff.F[i] <- 2*AdHzP[i-1,1]-AdHzP[i-2,1]
    AdHzP[i,1] ~ dnorm(diff.F[i],tauT)
}
for (i in 3:Nyrs) {
    diff.H[i] <- 2*AdHzP[1,i-1]-AdHzP[1,i-2]
    AdHzP[1,i] ~ dnorm(diff.H[i],tauT) 
}
for (i in 2:Npop) {
    for (j in 2:Nyrs) {
       diff.T[i,j] <- (AdHzP[i-1,j-1] + (AdHzP[i,j-1]-AdHzP[i-1,j-1]) 
                       + (AdHzP[i-1,j]-AdHzP[i-1,j-1]))			
       AdHzP[i,j] ~ dnorm(diff.T[i,j],tauT) 
    }
}
# Depth effect, inshore and offshore of modal depth
for (d in 1:NDeps){
  DPin[d] <- -1*B1*0.01*max(0,10-Depthvect[d])^2
  DPoff[d] <- -1*B2*0.01*max(0,Depthvect[d]-10)^2
}
# Fit priors (using vague cauchy priors for most params, as per Gelman 2008)
# Priors for K effects (expressed as log ratios): 
Intcpt ~ dt(0, 1/1^2 ,1) T(-3,3) # Mean K density, soft substrate (at modal depth)
IntcptE ~ dt(0, 1/1^2 ,1) T(-4,4) # Mean K density, estuary 
B1 ~ dt(0, 1/2.5^2 ,1) T(0,10) # Effect of Depth, inshore 
B2 ~ dt(0, 1/2.5^2 ,1) T(0,10) # Effect of Depth, offshore 
B3 ~ dt(0, 1/2.5^2 ,1) # Effect of substrate: proportion hard, linear
B4 ~ dt(0, 1/2.5^2 ,1) # Effect of kelp cover
B5 ~ dt(0, 1/2.5^2 ,1) # Effect of Shallow Slope (DistShore_v_Depth residual) 
# Dispersion param for depth bin counts (negative binomial distributions)
Nu ~ dt(0, .001 ,1) T(0.001,100) 
# Variance parameters:
# Note: uninformative half cauchy priors used for variance params (Gelman 2008) 
#  Variance in K density across coastal sections (log ratio of mean expected K)
sigK ~ dt(0, 1/1^2 ,1) T(0,) # 
tauK <- pow(sigK,-2)
# Environmental stochasticity (unexplained variance in annual hazard rates)
# Informed prior based on previous research results (telemetry studies)
# suggests sigE likely around 0.2 and cannot biologically be >1
sigE ~ dbeta(2,6)
tauE <- pow(sigE,-2)
# Observer error in otter counts: variance inflation factor for Poisson 
Vinflate <- 1.2
# Variance in shark mortality (over time and space)
sigT ~ dt(.5, 1/0.05^2 ,1) T(.2,1.5)# temporal variance in CAR variable logit(AdHz)
tauT <- pow(sigT,-2)
#
# Some nuiscense params
pupscalefact ~  dt(.35, 1/.1^2 ,1) T(0.1,.5) # % females with pups at survey(informed prior)
# Modal depth (depth otters most likely to be at, on average: part of depth function)
# Informed prior or fixed based on telemetry data (Tinker et al 2017)
ModalD <- 10 # ~ dnorm(10,1/3^2) T(1,20)
# Male bias in additional shark mortality hazards (1<Mbias multiplier<3)
Mbias <- dt(0, 1/2.5^2 ,1) T(0,) # 
# Proporion of females in male areas, transition areas and female dominated areas
propfem[1] ~ dbeta(1,10)
propfem[2] ~ dbeta(3,10)
propfem[3] ~ dbeta(10,6)
# Male Area Effect (reduces Female immigration relative to reproductive area)
FRD[1] ~ dbeta(1.5,1.5)
FRD[2] ~ dbeta(1.5,1.5)
FRDF[1] <- FRD[1]*.9 
FRDF[2] <- min(.95,FRDF[1]*(1/FRD[2])) 
FRDF[3] <- 1 
# "Urchin Boom Effect": reduction in hazards due to nutrional limitation,
#  effect of urchin boom (after seastar wasting) 2014-17 in central coast
UBE ~ dt(0, 1/5^2 ,1) T(-20,0)# Effect of Urchin boom (-ve for reduced hazard rate)
}