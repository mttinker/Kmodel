# Model to fit populatation growth with density dependence/stochasticity to 
# time series of survey counts (including habitat-specific K)
# *** CHANGES: collapse shark carcs by year/area, use binom probs
# step through depths (from 1:60) and estimate Depth adjustment var,
# using summed depth useage by area as observed node
model{
# Observed nodes: survey counts (loop through counts of adults and pups)
for (i in 1:Ncounts){
	ExpctInd[i] <- N[Ysrv[i],Psrv[i]]
	# Observed node: adult counts	  
	pparInd[i] <- Nu[3]/(Nu[3]+ExpctInd[i]) 
	CountI[i] ~ dnegbin(pparInd[i],Nu[3])		
	# CountI[i] ~ dpois(ExpctInd[i]) 
	#
	# Observed node: pup counts	  	
	# ExpctPups[i] <- max(1,n[1,max(1,Ysrv[i]-1),Psrv[i]])*fs[Psrv[i],max(1,Ysrv[i]-1)]*R[Psrv[i],max(1,Ysrv[i]-1)]*pupscalefact
	# ExpctPups[i] ~ dpois(ExpctPupsDet[i]) 
	# pparPup[i] <- NuPP/(NuPP+ExpctPups[i]) 
	# CountP[i] ~ dnegbin(pparPup[i],NuPP)		
}
# Observed Node: male and female carcasses (COD = 1 for shark, 0 for other)
for (i in 1:NCarcsF){
	OthHazF[i] <- exp(.8*zeta[2]+zeta[4]*PrpK[PshkF[i],YshkF[i]]+UB[PshkF[i],YshkF[i]]*UBE)
	ShkHazF[i] <- exp(.2*zeta[2]+AdHz[PshkF[i],YshkF[i]])
	ExpPshkF[i] <- ShkHazF[i]/(ShkHazF[i]+OthHazF[i])
	CarcShrkF[i] ~ dbin(ExpPshkF[i],TotCarcF[i])
}
for (i in 1:NCarcsM){
	OthHazM[i] <- exp(.8*zeta[2]+.7*zeta[3]+zeta[4]*PrpK[PshkM[i],YshkM[i]]+UB[PshkM[i],YshkM[i]]*UBE)
	ShkHazM[i] <- exp(.2*zeta[2]+.3*zeta[3]+Mbias*AdHz[PshkM[i],YshkM[i]])
	ExpPshkM[i] <- ShkHazM[i]/(ShkHazM[i]+OthHazM[i])
	CarcShrkM[i] ~ dbin(ExpPshkM[i],TotCarcM[i])
}
# Loop through Pops to initiate populations
for (i in 1:Npop){
	# Initialize pop vectors for SubPop i in year 1 (vague prior)
	Dns0[i] ~ dunif(0,5) # Uninformative uniform prior
	N[1,i] <- round(Dns0[i]*Area[i])	
	# Proportion of females depends on initial demographic composition
	NF0[i] ~ dbin(propfem[DemComp[i,1]],N[1,i])
	n[1,1,i] <- NF0[i] 
	n[2,1,i] <- N[1,i]-n[1,1,i]
	PrpK[i,1] <- N[1,i]/K[i]
}
# Loop through Pops and Years to compute population dynamics (Year 1 = 1983)
for (y in 1:(Nyrs-1)){
	for (i in 1:Npop){
		# Calculate effects of Environmental stochasticity
		eps[i,y] ~ dnorm(0,tauE) # env. stochasticity, variance in log hazard ratios
		epsR[i,y] = exp(eps[i,y]-(sigE^2)/2)  # Bias-adjusted log-normal error for R
		# Calculate vital rates, with effects of D-D, additional shark hazards, stochasticity
		#   and "urchin boom (UB) effect" in some areas/years
		R[i,y] = min(rho[1],((rho[2]^(-rho[3]*PrpK[i,y]))/10 + rho[4])*epsR[i,y])
        fs[i,y] = exp(-zeta[1]*exp(zeta[2]+zeta[4]*PrpK[i,y]+UB[i,y]*UBE+AdHz[i,y]+eps[i,y]))
        ms[i,y] = exp(-zeta[1]*exp(zeta[2]+zeta[3]+zeta[4]*PrpK[i,y]+UB[i,y]*UBE+Mbias*AdHz[i,y]+eps[i,y]))
		# Build projection matrix for year y, subpop i
		M[1,1,y,i] <- fs[i,y]*(1+R[i,y])
		M[1,2,y,i] <- 0
		M[2,1,y,i] <- fs[i,y]*R[i,y]         
		M[2,2,y,i] <- ms[i,y]
		# Demographic transitions, via Matrix multiplication
		n1[1:2,y,i] <- M[1:2,1:2,y,i]%*%n[1:2,y,i]
		nf1[i,y] <- n1[1,y,i]
		nm1[i,y] <- n1[2,y,i]
		# Create dispersal matrices, account for reduced female dispersal to male areas
		# DispAdj[i,y] <- ifelse(MalArea[i,y]==1,MAeff,1)
		# DAf[i,1:Npop,y] <-DispAdj[i,y]*disprobF[i,1:Npop]
		DAf[i,1:Npop,y] <- FRDF[DemComp[i,y]]*disprobF[i,1:Npop] 
		#DRf[1:Npop,i,y] ~ ddirch(50*DAf[1:Npop,i,y])
		DRf[1:Npop,i,y] <- DAf[1:Npop,i,y]/sum(DAf[1:Npop,i,y])
		DAm[i,1:Npop,y] <- disprobM[i,1:Npop] 
		#DRm[1:Npop,i,y] ~ ddirch(25*DAm[1:Npop,i,y])
		DRm[1:Npop,i,y]	<- DAm[1:Npop,i,y]/sum(DAm[1:Npop,i,y])
	}
	# Redistribute males and females: multiply by dispersal matrices
	nf[1:Npop,y] <- DRf[1:Npop,1:Npop,y]%*%nf1[1:Npop,y]
	nm[1:Npop,y] <- DRm[1:Npop,1:Npop,y]%*%nm1[1:Npop,y]	
	for (i in 1:Npop){
		n[1,y+1,i] <- nf[i,y]
		n[2,y+1,i] <- nm[i,y]
		N[y+1,i] <- n[1,y+1,i]+n[2,y+1,i]
		# Calculate proportion of K for SubPop i in year y,
		PrpK[i,y+1] <- N[y+1,i]/K[i]
	}	
}
# Loop through SubPops to estimate K (summed avgerage of grid cells)
for (i in 1:Npop) {
  # Allow for variance in realized K density across coastal sections 
  # (assume log-normal distribution in K density)
  epsK[i] ~ dnorm(0,tauK)
  for (j in 1:NgrdH[i]){
  # Loop through grid cells to estimate localized K in high dens strata
    # DepEffH[j,i] <- ifelse(DepH[j,i]<=ModalD,(1-((DepH[j,i]-ModalD)/100)^2)^B1 - 1,(1-((DepH[j,i]-ModalD)/100)^2)^B2 -1)
    KgHD[j,i] <- ifelse(SubH[j,i]==4,exp(B0[SubH[j,i]]+epsK[i]),exp(B0[SubH[j,i]]+log(DepEff[DepH[j,i]])+B3*KlpH[j,i]+B4*DshH[j,i]+epsK[i])) 
	ExpctCountH[j,i] <- sum(PrpK[i,1:Nyrs])*KgHD[j,i]*AreaGH[j,i]
	# Observed node: grid count in HD cell	  
	pparHD[j,i] <- Nu[1]/(Nu[1]+ExpctCountH[j,i]) 
	GridcountH[j,i] ~ dnegbin(pparHD[j,i],Nu[1])	
  }
  for (j in 1:NgrdL[i]){
  # Loop through grid cells to estimate localized K in low dens strata
    # DepEffL[j,i] <- ifelse(DepL[j,i]<=ModalD,(1-((DepL[j,i]-ModalD)/100)^2)^B1,(1-((DepL[j,i]-ModalD)/100)^2)^B2)
    KgLD[j,i] <- exp(B0[SubL[j,i]]+log(DepEff[DepL[j,i]])+B3*KlpL[j,i]+B4*DshL[j,i]+epsK[i])
	ExpctCountL[j,i] <- sum(PrpK[i,1:Nyrs])*KgLD[j,i]*AreaGL[j,i]
	# Observed node: grid count in LD cell	  
	pparLD[j,i] <- Nu[2]/(Nu[2]+ExpctCountL[j,i]) 
	GridcountL[j,i] ~ dnegbin(pparLD[j,i],Nu[2])		  
  }
  # K[i] <- AreaHD[i]*mean(KgHD[1:NgrdH[i],i])+AreaLD[i]*mean(KgLD[1:NgrdL[i],i])  
  K[i] <- sum(KgHD[1:NgrdH[i],i]*PrpnAH[1:NgrdH[i],i])*AreaHD[i] + sum(KgLD[1:NgrdL[i],i]*PrpnAL[1:NgrdL[i],i])*AreaLD[i]
}
# Compute depth effect function and fit to observed summed counts at each depth
for (i in 1:Ndeps) {
	DepPar[i] <- ifelse(i<=ModalD,B1,B2)
	DepEff[i] <- (1-((i-ModalD)/100)^2)^DepPar[i] 
	ExpCountDep[i] <-(NcellDep[i]/DepProbAdj)*DepEff[i]
	CountDep[i] ~ dpois(ExpCountDep[i])
}
# Compute additional Shark bite hazards by sub-pop and year
#  (as Conditional Autoregressive variable in 2 dimensions, coast section and time)
for (y in 1:(Nyrs)){
	for (i in 1:Npop){
		AdHz[i,y] <- ifelse(i==5,0,5*ilogit(AdHzP[i,y]))
	}
}
AdHzP[1,1] ~ dt(-5, 1/2^2 ,1) 
AdHzP[2,1] ~ dt(-5, 1/2^2 ,1) 
AdHzP[1,2] ~ dt(-5, 1/2^2 ,1) 
for (i in 3:Npop) {
    diff.F[i] <- 2*AdHzP[i-1,1]-AdHzP[i-2,1]
    AdHzP[i,1] ~ dnorm(diff.F[i],tauT)
}
for (i in 3:Nyrs) {
    diff.H[i] <- 2*AdHzP[1,i-1]-AdHzP[1,i-2]
    AdHzP[1,i] ~ dnorm(diff.H[i],tauT) 
}
for (i in 2:Npop) {
    for (j in 2:Nyrs) {
       diff.T[i,j] <- (AdHzP[i-1,j-1] + (AdHzP[i,j-1]-AdHzP[i-1,j-1]) 
                       + (AdHzP[i-1,j]-AdHzP[i-1,j-1]))
	   # diff.T[i,j] <- ifelse(TP[i,j]==1,max(1,diff.Tstar[i,j]),diff.Tstar[i,j])				
       AdHzP[i,j] ~ dnorm(diff.T[i,j],tauT) 
    }
}
# Fit priors (vague cauchy priors used for most params)
# Priors for K effects:
B0[1] ~ dt(0, 1/2.5^2 ,1) # Modal K density, soft substrate
B0[2] ~ dt(0, 1/2.5^2 ,1) # Modal K density, mixed substrate
B0[3] ~ dt(0, 1/2.5^2 ,1) # Modal K density, hard substrate
B0[4] ~ dt(0, 1/2.5^2 ,1) # Modal K density, estuary 
B1 ~ dt(0, 1/10^2 ,1) T(0,100) # Effect of Depth, inshore of modal depth 
B2 ~ dt(0, 1/10^2 ,1) T(0,100) # Effect of Depth, offshore of modal depth 
B3 ~ dt(0, 1/2.5^2 ,1) # Effect of Kelp (log ratio)
B4 ~ dt(0, 1/2.5^2 ,1) # Effect of Shallow Slope (DistShore_v_Depth residual), log ratio
# Dispersion params for obserbed vars (negative binomial distributions)
Nu[1] ~ dt(0, .001 ,1) T(0.001,100) # Dispersion parameter, variance in counts HD grid cell
Nu[2] ~ dt(0, .001 ,1) T(0.001,100) # Dispersion parameter, variance in counts LD grid cell
Nu[3] ~ dt(0, .001 ,1) T(0.001,100) # Dispersion parameter, observer error in counts
# Note: uninformative half cauchy priors used for variance params (Gelman 2008) 
#  Variance in K density across coastal sections (log ratio of mean expected K)
sigK ~ dt(0, 1/2.5^2 ,1) T(0,10) # 
tauK <- pow(sigK,-2)
# Environmental stochasticity (unexplained variance in annual hazard rates)
sigE ~ dt(0, 1/2.5^2 ,1) T(0,10) # 
tauE <- pow(sigE,-2)
# Variance in shark mortality (over time and space)
sigT ~ dt(0, 1/5^2 ,1) T(0,20) # temporal variance term for CAR logit(AdHz)
tauT <- pow(sigT,-2)
#
# Some nuiscense params
# pupscalefact ~ dnorm(1.1,1/.2^2) T(0.5,1.8) # Adjusts for % pups present at survey
# NuPP <- 0.5*Nu[1] 
# Modal depth (depth otters most likely to be at, on average: part of depth function)
ModalD ~ dnorm(12,1/3^2) T(4,20)
DepProbAdj ~ dbeta(2,1.5)
# Male bias in additional shark mortality hazards (positive multiplier)
MbiasInv ~ dbeta(8,3)
Mbias <- 1/MbiasInv
# Male Area Effect (adjusts Female dispersal relative to reproductive area)
FRD[1] ~ dbeta(1.5,1.5)
FRD[2] ~ dbeta(1.5,1.5)
FRDF[1] <- FRD[1]/1.5 
FRDF[2] <- min(1,FRDF[1]+FRD[2]) 
FRDF[3] <- 1 
# "Urchin Boom Effect", reduction in hazards due to nutrional limitation
# (allows for effect of urchin boom (seastar wasting) 2014-17 in central coast)
UBE ~ dt(0, 1/5^2 ,1) T(-20,0)# Effect of Urchin boom (-ve for reduced hazard rate)
}